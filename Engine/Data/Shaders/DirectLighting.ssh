#ifndef DIRECT_LIGHTING_HEADER
#define DIRECT_LIGHTING_HEADER
//#pragma once

#include "ShadingModels.ssh"
#include "LightsCommon.ssh"
#include "LightCullingCommon.ssh"
#include "Shadows.ssh"

#define DEBUG_CASCADE_COLOR (0)
#define DEBUG_LIGHT_CULLING (0)

float3 CalculateDirectionalLightContribution(in const MaterialData aMaterialData, in const float3 aWorldPosition, in const float3 aViewDirection, in float aPixelDepth)
{
    const EnvironmentConstants environmentConstants = gSceneConstants.mEnvironmentConstants;
    float3 lightDirection = environmentConstants.mSunLightDirection;
    float3 lightColor = environmentConstants.mSunLightColor;
    float lightRadiance = environmentConstants.mSunLightIntensity;

    float3 lighting = (float3)0.0f;

#if DEBUG_CASCADE_COLOR
    float3 debugColor = (float3)0.0f;
    float shadowFactor = CalculateCascadedShadowFactor(aWorldPosition, aPixelDepth, debugColor);
    lighting = debugColor;
#else
    float shadowFactor = CalculateCascadedShadowFactor(aWorldPosition, aPixelDepth);
#endif

    if (shadowFactor <= 0.0f)
        return lighting;

    float nDotL = saturate(dot(aMaterialData.mWorldNormal, lightDirection));
    float3 shading = CalculateShading(aMaterialData, (SR_AreaLight)0, lightDirection, nDotL, 1.0f, aViewDirection);
    lighting += (shading * lightColor * lightRadiance * shadowFactor);

    return lighting;
}

float GetInverseSquareFalloff(float aDistanceSqr, float aRange)
{
	return Squared(saturate(1.0f - Squared(aDistanceSqr * Squared(aRange))));
}

/** 
 * Calculates attenuation for a spot light.
 * L normalize vector to light. 
 * SpotDirection is the direction of the spot light.
 * SpotAngles.x is CosOuterCone, SpotAngles.y is InvCosConeDifference. 
 */
float GetSpotAttenuationMask(float3 L, float3 aSpotDirection, float2 aSpotAngles)
{
	return saturate((dot(L, -aSpotDirection) - aSpotAngles.x) * aSpotAngles.y);
}
float GetSpotAttenuation(float3 L, float3 aSpotDirection, float2 aSpotAngles)
{
	float coneAngleFalloff = Squared(GetSpotAttenuationMask(L, aSpotDirection, aSpotAngles));
	return coneAngleFalloff;
}

// Alpha is half of angle of spherical cap
float SphereHorizonCosWrap(float nDotL, float aSinAlphaSqr)
{
	float sinAlpha = sqrt( aSinAlphaSqr );
	if(nDotL < sinAlpha)
	{
		nDotL = max( nDotL, -sinAlpha );
		// Hermite spline approximation
		// Fairly accurate with SinAlpha < 0.8
		// y=0 and dy/dx=0 at -SinAlpha
		// y=SinAlpha and dy/dx=1 at SinAlpha
		nDotL = Pow2(sinAlpha + nDotL) / (4.0f * sinAlpha);
	}
	return nDotL;
}

float GetLocalLightAttenuation(in const SR_LocalLight aLight, in const float3 aWorldPosition, in const float3 L, in const float aDistanceSqr)
{
    float attenuation = GetInverseSquareFalloff(aDistanceSqr, aLight.mInvRange);

    if (aLight.mType == SR_LightType_Spot)
    {
        attenuation *= GetSpotAttenuation(L, aLight.mDirection, aLight.mSpotAngles);
    }

    return attenuation;
}

float3 CalculateLocalLightContribution(in const MaterialData aMaterialData, in const SR_LocalLight aLight, in const float3 aWorldPosition, in const float3 aViewDirection)
{
    float3 toLight = aLight.mPosition - aWorldPosition;
    float distSqr = dot(toLight, toLight);
    float invDist = rsqrt(distSqr);
    float3 L = toLight * invDist;

    float attenuation = GetLocalLightAttenuation(aLight, aWorldPosition, L, distSqr);

    float3 contribution = (float3)0;
    [branch]
    if (attenuation > 0.0f)
    {
        // GetShadowFactor
        // Branch if shadow is > 0

        float3 lightColor = aLight.mColoredIntensity;

	    SR_AreaLight areaLight;
        {
		    float falloff = rcp(distSqr /*+ distBiasSqr*/);

		    float nDotL = dot(aMaterialData.mWorldNormal, L);
		    float sinAlphaSqr = saturate(Pow2(aLight.mSourceRadius) * falloff);
		    nDotL = SphereHorizonCosWrap(nDotL, sinAlphaSqr);
	        nDotL = saturate(nDotL);

	        float a = Pow2(aMaterialData.mRoughness);

            areaLight.mSphereSinAlpha = saturate(aLight.mSourceRadius * invDist * (1.0f - a));
            areaLight.mSphereSinAlphaSoft = saturate(aLight.mSourceRadius * invDist);
            areaLight.mNdotL = nDotL;
            areaLight.mFalloff = falloff;
            areaLight.mLightVector = L;
        }

        float3 shading = CalculateShading(aMaterialData, areaLight, areaLight.mLightVector, areaLight.mNdotL, areaLight.mFalloff, aViewDirection);
        return shading * aLight.mColoredIntensity * attenuation;
    }

    return contribution;
}

#if DEBUG_LIGHT_CULLING
float3 GetTileCostColor(uint aNumLights)
{
    if (aNumLights > 60)
        return float3(1.0f, 0.0f, 1.0f);
    if (aNumLights > 32)
        return float3(1.0f, 0.0f, 0.0f);
    if (aNumLights > 16)
        return float3(1.0f, 1.0f, 0.0f);
    if (aNumLights > 8)
        return float3(0.0f, 1.0f, 0.0f);
    if (aNumLights > 4)
        return float3(0.0f, 0.0f, 1.0f);
    if (aNumLights > 0)
        return float3(0.75f, 0.75f, 0.75f);
    
    return float3(0.25f, 0.25f, 0.25f);
}
#endif

float3 CalculateDirectLighting(in MaterialData aMaterialData, in const float3 aWorldPosition, in const float3 aViewDirection, in float aPixelDepth)
{
    float3 lighting = (float3)0.0f;

    // Should we only support one directional light?
    lighting += CalculateDirectionalLightContribution(aMaterialData, aWorldPosition, aViewDirection, aPixelDepth);

    const SR_TileData tileData = GetTileData(aMaterialData.mClipPosition.xy);
    for (uint i = 0; i < tileData.mNumLights; ++i)
    {
        const SR_LocalLight light = GetLocalLight(tileData.mLightIndices[i]);
        lighting += CalculateLocalLightContribution(aMaterialData, light, aWorldPosition, aViewDirection);
    }

#if DEBUG_LIGHT_CULLING
    lighting += GetTileCostColor(tileData.mNumLights);
#endif

    return lighting;
}

#endif //DIRECT_LIGHTING_HEADER